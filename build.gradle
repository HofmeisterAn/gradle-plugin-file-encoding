plugins {
	id "com.diffplug.gradle.spotless" version "3.10.0"
	id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven-publish'

// Configuration that adds and holds the plugin dependencies into the jar
configurations { pluginDependency }

repositories { jcenter() }

dependencies {
	compile gradleApi()
	compile localGroovy()

	testCompile gradleTestKit()
	testCompile 'junit:junit:4+'

	// Add plugin dependencies here e. g. pluginDependency 'junit:junit:4+'
	configurations.compile.extendsFrom(configurations.pluginDependency)
}

sourceSets {
	main {
		java { srcDirs = ['src/main/java']}
		groovy { srcDirs = ['src/main/groovy']}
		resources { srcDirs = ['src/main/resources']}
	}

	test {
		java { srcDirs = ['src/test/java']}
		groovy { srcDirs = ['src/test/groovy']}
		resources { srcDirs = ['src/test/resources']}
	}
}

spotless {
	java { googleJavaFormat() }
	groovy { greclipse() }
	groovyGradle { greclipse() }
}

jar {
	from {
		configurations.pluginDependency.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

bintray {
	publish = true
	override = true
	configurations = ['archives']
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	pkg {
		repo = 'maven'
		name = project.name
		licenses = ['MIT']
		vcsUrl = 'https://github.com/padme-amidala/gradle-plugin-file-encoding.git'
		version {
			name = project.version
			released  = new Date()
		}
	}
}
